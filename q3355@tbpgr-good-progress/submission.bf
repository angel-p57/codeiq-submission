* parse *
,+[-
 >++++++++++++<[->-[>+>>]>[+[-<+>]>+>>]<<<<<]
 >[-]>>[[-]
  <<<<[->>>++++++++++<<<]
  >>>>,+>>
 ]
 <[,+<]
<]
<[>[->>>]<[>+>+>]<<<-]
>[-
 * create dot o \n *
 >>->>>>>>>+++++++++++[->>--->+++>+<[+++++++<]<]>>++>+>[-<+]
 * two halves loop *
 >++[
  -[>]<<[
   * middle *
   -[->+>>>>>>>>++<<<<<<<<<]
   +>[-<+>]->>>>>>>>>>.<<[-
    [>.<<<]
    >[>.<<<]
   >]
   >>>.+[-<+]
  ]
  * copy the IV of the counter *
  <-[->+>>+<<<]+>[-<+>]
  * loop for each line *
  >>[
   * setup character numbers *
   <[
    >[->>+>>>+<<<<<]
    >[->>+>>>++<<<<<]
   <]
   <<[
    >>
    >-[->>+>>>>++<<<<<<]
    >+[->>+>>+<<<<]
    >>-<+<
    <<<
   ]
   * move back counters *
   >>>>-[-<<+>>]>[-<<+>>]
   * print one line *
   >>[-<<+>>>>.<<]>>>.<<[-[>.<<<]>[>.<<<]>]<<<[->>>>.<<<<]>>>>>>.<<<<<<<<+<
  ]
  >,+
 <<]
,+<,+]
>[
 * invalid *
 ,---------[+>+>>+>+>++>+++[+++++++++<]<<]
 >>>>+++++.>.>--.<<<---.>>--.<.<+++.<<.<
]
