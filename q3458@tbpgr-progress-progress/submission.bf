* parse *
,+[-
 >++++++++++++<[->-[>+>>]>[+[-<+>]>+>>]<<<<<]
 >[-]>>[[-]
  <<<<[->>>++++++++++<<<]
  >>>>,+>>
 ]
 <[,+<]
<]
* divide by 2 *
<[>[->>>]<[>+>+>]<<<-]
>[
 * two halves loop *
 +[-
  * move and copy * 
  >[->+>+>++<<<]
  <[->+>>-<<<]
  >>>>-<
  * each line loop *
  [-
   * copy and move n and then move back *
   >[->>+>+<<<]
   >>[-<<+>>]
   <[-
    * not the first line of the half *
    * print the first p and dots ( except the last one ) *
    >>>.<--[->>.<<]
    <<<<<<[
     * the first half *
     +[->+]<<<.
    >>>]
    >[
     * the second half *
     +[->+]<<.
    >>>]
    * print \n *
    <-<.<<<<<
   >]
   <[
    * the first line of the half *
    * create dot p \n and print *
    >>+++++++++++[->>++++++++++>++++>+<<<<]
    >>++<[->.<]>>++>-.>-
   <<<<<<]
   * next: not the first *
   +
  <<]
  * clear ( be carefull when n=1 ) *
  >,+>[,>+[,+>+]+[-<+]]
 <<<<]
<]
>[
 * invalid *
 ,---------[+>+>>+>+>++>+++[+++++++++<]<<]
 >>>>+++++.>.>--.<<<---.>>--.<.<+++.<<.<
]
