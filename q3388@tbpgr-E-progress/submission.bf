* parse *
,+[-
 >++++++++++++<[->-[>+>>]>[+[-<+>]>+>>]<<<<<]
 >[-]>>[[-]
  <<<<[->>>++++++++++<<<]
  >>>>,+>>
 ]
 <[,+<]
<]
<[>[->>>]<[>+>+>]<<<-]
>[-
 * create dot e \n *
 >>->>>>>>>+++++++++++[->>->++++>+<[+++++<]<]>>++>++>[-<+]
 * two halves loop *
 >++[
  -[>]<<[
   * middle *
   [->+>>>>>>>+<<<<<<<<]
   >[-<+>]-
   >>>>>>>[->+>>.<<<]
   >-[->.<]
   >>>.+[-<+]
  ]
  * copy the IV of the counter *
  <-[->+>>+<<<]+>[-<+>]
  * loop for each line *
  >>[#
   * detect edges *
   <[
    >>>+
    <[>->>]
    <[>>>]
   <<<<<]
   <<[
    >>>>>+
    <<-[>>->-<]
    >+[->>]
    <<<+
   <<<<]
   * copy the width *
   >-[->+>>>>>>>>++<<<<<<<<<]>+[-<+>]
   * print one line *
   >>>>[->->>>+[->>.<<]>>>.<<<<<<<]
   >+[->>>>>.<<[->.<]>>>.<<<<<<]
   <<+<-
  ]
  >,+
 <<]
,+<,+]
>[
 * invalid *
 ,---------[+>+>>+>+>++>+++[+++++++++<]<<]
 >>>>+++++.>.>--.<<<---.>>--.<.<+++.<<.<
]
