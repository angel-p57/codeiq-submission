* parse *
,+[-
 >++++++++++++<[->-[>+>>]>[+[-<+>]>+>>]<<<<<]
 >[-]>>[[-]
  <<<<[->>>++++++++++<<<]
  >>>>,+>>
 ]
 <[,+<]
<]
<[>[->>>]<[>+>+>]<<<-]
>[-
 * create dot x \n *
 >>->>>>>>>++++++++++++[->>--->+++>+<[+++++++<]<]>>-->>>-<-[-<+]
 * two halves loop *
 >++[
  -[>]<<[
   * middle *
   -[->->++[->+]-<<<.+[-<+]<]+
   >->+[->+]-<<.+[-<+]
   >[->->+[->+]-<<<.+[-<+]<<<+>>]
   ->+[->+]-<.+[-<+]<
  ]
  * copy the IV of the counter *
  <-[->+>>+<<<]+>[-<+>]
  * loop for each line *
  >>[
   * setup character numbers *
   <[
    >[->>+>>>>++<<<<<<]
    >[->>+>+>+<<<<]
   <]
   <<[
    >>>-[->>+>>+>+<<<<<]
    >+[->>+>>>++<<<<<]
    >>-<+<<<<
   ]
   * print one line *
   >>>->>>[->>>.<<<]>>>>.<<-[->.<]>>.<<<[->>.<<]>>>>.+[-<+]
   * move back counters *
   >>[-<<+>>]<-[-<<+>>]
  <+<]
  >,+
 <<]
,+<,+]
>[
 * invalid *
 ,---------[+>+>>+>+>++>+++[+++++++++<]<<]
 >>>>+++++.>.>--.<<<---.>>--.<.<+++.<<.<
]
